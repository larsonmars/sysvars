; $VER: SYSVARS ECHO Test Script 0.11 (18.11.2023)
ksge36
IF WARN
  ECHO "Setting things up for KS 1.x..." NOLINE
  IF EXISTS C:RESIDENT ; Run under Workbench
    RESIDENT ECHO  C:ECHO  pure
    RESIDENT IF    C:IF    pure
    RESIDENT ELSE  C:ELSE  pure
    RESIDENT ENDIF C:ENDIF pure
  ELSE
    IF EXISTS OS1.3-Support
      COPY >NIL: OS1.3-Support RAM: ALL
      RAM:C/ASSIGN C: RAM:C
    ELSE
      ECHO No OS 1.3 Support available
    ENDIF
  ENDIF
  ASSIGN >NIL: ENV: EXISTS
  IF WARN
    MAKEDIR RAM:Env
    ASSIGN ENV: RAM:ENV
  ENDIF 
  ECHO " done."
ENDIF

ECHO "*E[0;0H*E[J"
ECHO "             ___ _    __ _    _     ____  ____"
ECHO "            / __| |  / _| |  | |/\ |  _ \/ ___)"
ECHO "           | (__| | | (_| |  | /  \| | \| (__ "
ECHO "            \__  \ \/\__| |  |/ /\ | |_/ \__ \"
ECHO "               ) )\ /   )\ \// ____|  _ (   ) )"
ECHO "            __/ // / __/ /\ / /    | | \ \_/ /"
ECHO "           (___//_/ |___/  V_/     |_|  (___/ "
ECHO ""

sysvars

ECHO "-------------------------------------------------------------"
ECHO "                        Results:"
ECHO "-------------------------------------------------------------"
ECHO ""

IF $KickVer GE 36
  IF $KickVer GE 37
    IF $KickVer GE 39
      IF $KickVer GE 40
        IF $KickVer GE 44
          IF $KickVer GE 45
            IF $KickVer GE 46
              IF $KickVer GE 47
                IF $KickVer GE 48
                  IF $KickVer GE 49
                    IF $KickVer GE 50
                      ECHO "Amiga OS 4+ or MorphOS detected (Kickstart version $KickVer.$KickRev)"
                    ELSE
                      ECHO "Amiga OS 3.4(?) detected (Kickstart version $KickVer.$KickRev)"
                    ENDIF
                  ELSE
                    ECHO "Amiga OS 3.3(?) detected (Kickstart version $KickVer.$KickRev)"
                  ENDIF
                ELSE
                  ECHO "Amiga OS 3.2 detected (Kickstart version $KickVer.$KickRev)"
                ENDIF
              ELSE
                IF $KickRev EQ 143
                  ECHO "Amiga OS 3.1.4 detected (Kickstart version $KickVer.$KickRev)"
                ELSE
                  ECHO "AROS detected (Kickstart version $KickVer.$KickRev)"
                ENDIF
              ENDIF
            ELSE
              ECHO "Amiga OS 3.9 detected (Kickstart version $KickVer.$KickRev)"
            ENDIF
          ELSE
            ECHO "Amiga OS 3.5 detected (Kickstart version $KickVer.$KickRev)"
          ENDIF
        ELSE
          ECHO "Amiga OS 3.1 detected (Kickstart version $KickVer.$KickRev)"
        ENDIF
      ELSE
        ECHO "Amiga OS 3.0 detected (Kickstart version $KickVer.$KickRev)"
      ENDIF
    ELSE
      IF $KickRev GE 71
        ECHO "Amiga OS 2.05 detected (Kickstart version $KickVer.$KickRev)"
      ELSE
        ECHO "Amiga OS 2.04 detected (Kickstart version $KickVer.$KickRev)"
      ENDIF
    ENDIF
  ELSE
    ECHO "Amiga OS 2.0 detected (Kickstart version $KickVer.$KickRev)"
  ENDIF

  IF "$CPU" EQ ""
    ECHO "CPU: not detected"
  ELSE
    ECHO "CPU: $CPU"
  ENDIF
  IF "$FPU" EQ ""
    ECHO "FPU: not detected"
  ELSE
    ECHO "FPU: $FPU"
  ENDIF
  ECHO "Chipset: $Chipset" NOLINE

ELSE

  IF $KickVer GE 34
    ECHO "Amiga OS 1.3 detected"
  ELSE
    IF $KickVer EQ 33
      ECHO "Amiga OS 1.2 detected"
    ELSE
      IF $KickVer EQ ""
        ECHO "Kickstart not detected"
      ELSE
        ECHO "Kickstart below 1.3 detected (unsupported)"
      ENDIF
    ENDIF
  ENDIF

  IF $CPU EQ 68000
    ECHO "CPU: 68000"
  ELSE
    IF $CPU EQ 68010
      ECHO "CPU 68010"
    ELSE
      IF $CPU EQ 68020
        ECHO "CPU: 68020"
      ELSE
        IF $CPU EQ 68030
          ECHO "CPU: 68030"
        ELSE
          IF $CPU EQ 68040
            ECHO "CPU: 68040"
          ELSE
            IF $CPU EQ 68060
              ECHO "CPU: 68060"
            ELSE  
              IF $CPU EQ 68080
                ECHO "CPU: 68080"
              ELSE
                IF $CPU EQ ""
                  ECHO "no CPU detected"
                ELSE
                  ECHO "Unknown CPU"
                ENDIF
              ENDIF
            ENDIF
          ENDIF
        ENDIF
      ENDIF
    ENDIF
  ENDIF

  IF $FPU EQ "internal"
    ECHO Internal FPU
  ELSE
    IF $FPU EQ "68881"
      ECHO "FPU: 68881"
    ELSE
      IF $FPU EQ "68882"
        ECHO "FPU: 68882"
      ELSE
        IF $FPU EQ ""
          ECHO "FPU: none detected"
        ELSE
          ECHO "Unknown FPU"
        ENDIF
      ENDIF
    ENDIF
  ENDIF

  IF $Chipset EQ SAGA
    ECHO "Chipset: SAGA" NOLINE
  ELSE
    IF $Chipset EQ AGA
      ECHO "Chipset: AGA" NOLINE
    ELSE
      IF $Chipset EQ ECS
        ECHO "Chipset: ECS" NOLINE
      ELSE
        IF $Chipset EQ OCS
          ECHO "Chipset: OCS" NOLINE
        ELSE
          ECHO "Chipset: Unknown" NOLINE
        ENDIF
      ENDIF
    ENDIF
  ENDIF
ENDIF

IF $VFreq EQ 50
  ECHO " PAL (50Hz)"
ELSE
  IF $VFreq EQ 60
    ECHO " NTSC (60Hz)"
  ELSE
    ECHO " (unknown vertical frequency)"
  ENDIF
ENDIF

IF $CPU EQ 68080
  IF $VampireType EQ V2_600
    ECHO "Detected Vampire V2 600"
  ELSE
    IF $VampireType EQ V2_500
      ECHO "Detected Vampire V2 500"
    ELSE
      IF $VampireType EQ V4_500/1000/2000
        ECHO "Detected Vampire V4 Firebird/Phoenix/Salamander"
      ELSE
        IF $VampireType EQ V4_1200
          ECHO "Detected Vampire V4 Icedrake"
        ELSE
          IF $VampireType EQ V4_Standalone
            ECHO "Detected Vampire V4 Standalone"
          ELSE
            IF $VampireType EQ V2_1200
              ECHO "Detected Vampire V2 1200"
            ELSE
              IF $VampireType EQ V4_600
                ECHO "Detected Vampire V4 Manticore"
              ELSE
                IF $VampireType EQ unknown
                  ECHO "Detected Vampire of unknown type"
                ENDIF
              ENDIF
            ENDIF
          ENDIF
        ENDIF
      ENDIF
    ENDIF
  ENDIF

  IF $KickVer GE 36
    IF NOT "$VampireCoreRev" EQ ""
      ECHO "Vampire Core Revision: $VampireCoreRev"
    ENDIF
    IF NOT "$VampireClockMult" EQ ""
      ECHO "Vampire Clock Multiplier: $VampireClockMult"
    ENDIF
  ENDIF
ENDIF

IF "$BSDSockLib" EQ ""
  ECHO "bsdsocket.library not available"
ELSE
  IF $KickVer GE 36
    ECHO "bsdsocket.library V$BSDSockLibVer.$BSDSockLibRev named *"$BSDSockLib*" detected"
  ELSE
    ECHO "bsdsocket.library detected"
  ENDIF
ENDIF

IF "$UAEMajor" EQ ""
  ECHO "No UAE detected"
ELSE
  IF $KickVer GE 36
    ECHO "UAE V$UAEMajor.$UAEMinor.$UAERev detected"
  ELSE
    ECHO "UAE detected"
  ENDIF
ENDIF
